plugins {
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlin_version" // For test purposes
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version "$bintray_version"
}

dependencies {
    api project(':serialization')
    api "org.mongodb:bson:$mongo_version"
}

compileKotlin {
    kotlinOptions.freeCompilerArgs = ['-Xopt-in=kotlin.RequiresOptIn']
}

java {
    withSourcesJar()
    withJavadocJar()
}

bintray {
    user = 'agcom'
    key = System.env.BINTRAY_KEY
    publications = ['bsonMongodb']
    pkg {
        repo = 'bson'
        name = 'mongodb'
        version {
            name = project.version.toString()
            desc = 'Patch to deal with internal and deprecated bson types'
            released = new Date()
            vcsTag = "v${project.version.toString()}"
        }
    }
}

publishing {
    publications {
        bsonMongodb(MavenPublication) {
            artifactId = 'bson-mongodb'
            pom {
                packaging = 'jar'
                withXml {
                    def root = asNode()
                    new XmlParser().parse(buildFile.toPath().parent.resolve('pom-meta.xml').toFile()).each {
                        root.append(it)
                    }
                }
            }
            from components.java
        }
    }
}