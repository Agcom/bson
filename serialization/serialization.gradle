plugins {
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlin_version" // For test purposes
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version "$bintray_version"
}

dependencies {
    api "org.mongodb:bson:$mongo_version"
}

compileKotlin {
    kotlinOptions.freeCompilerArgs = ['-Xopt-in=kotlin.RequiresOptIn']
}

java {
    withSourcesJar()
    withJavadocJar()
}
bintray {
    user = 'agcom'
    key = System.env.BINTRAY_KEY
    publications = ['bsonSerialization']
    pkg {
        repo = 'bson'
        name = 'serialization'
        version {
            name = project.version.toString()
            desc = 'First release'
            released = new Date()
            vcsTag = "v${project.version.toString()}"
        }
    }
}

publishing {
    publications {
        bsonSerialization(MavenPublication) {
            artifactId = 'bson-serialization'
            pom {
                def git = 'https://github.com/agcom/bson'
                packaging = 'jar'
                name = 'bson-serialization'
                description = 'Bson implementation for Kotlinx serialization'
                url = git
                inceptionYear = '2020'
                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'agcom'
                        name = 'Alireza Ghasemi'
                        email = 'agcombest@gmail.com'
                        url = 'https://github.com/agcom'
                    }
                }
                scm {
                    connection = "${git}.git"
                    developerConnection = connection
                    url = git
                }
                issueManagement {
                    system = 'Github Issues'
                    url = "$git/issues"
                }
                ciManagement {
                    system = 'Github Actions'
                    url = "$git/actions"
                }
                distributionManagement {
                    downloadUrl = 'https://bintray.com/agcom/bson/serialization'
                }
            }
            from components.java
        }
    }
}